apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'dagger.hilt.android.plugin'

apply from: '../ktlint.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = appBuildNumber.replace(".", "").toInteger()

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.ottoboni.movies"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }

        buildConfigField 'String', 'TMDB_BASE_URL', '"https://api.themoviedb.org"'
        buildConfigField 'String', 'TMDB_API_QUERY', '"api_key"'
        buildConfigField 'String', 'TMDB_API_KEY', "${project.properties['apiKey']}"
        buildConfigField 'String', 'TMDB_API_VERSION', '"3"'
        buildConfigField 'String', 'TMDB_BASE_IMGAGE_URL', '"https://image.tmdb.org/t/p/original/"'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation deps.kotlin

    // Android Support
    implementation deps.android_ui
    implementation deps.androidx

    //Hilt
    implementation deps.hilt
    kapt deps.hilt_processor

    // Architecture components
    implementation deps.lifecycle
    kapt deps.lifecycle_compiler

    // Database
    implementation deps.database
    testImplementation deps.database_testing
    kapt deps.database_compiler

    // Kotlin Coroutines
    implementation deps.coroutines
    testImplementation deps.coroutines_testing

    // Network
    implementation deps.network
    testImplementation deps.network_testing

    // Arch Core
    testImplementation deps.arch_core
    testImplementation deps.arch_core_testing

    // Unit Testing
    testImplementation deps.testing

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.android_testing

    // Debug
    implementation deps.stetho
    implementation deps.timber
    implementation deps.leakcanary
}
